buildscript {
    ext {
        jacocoVersion = "0.8.7"
        androidXLifecycleVersion = "2.3.1"
        glideVersion = '4.11.0'
        fragment_version = "1.3.6"
        roomVersion = "2.4.0"
        billing_version = "4.1.0"
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.hiya.jacoco-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id("com.github.triplet.play") version "3.7.0"
    id 'com.starter.easylauncher'
}

android {
    signingConfigs {
        release
    }

    compileSdk 31

    Properties appProps = new Properties()
    def appPropsFile = new File('application.properties')

    if (appPropsFile.canRead()) {
        appProps.load(new FileInputStream(appPropsFile))
    } else {
        println 'application.properties not found'
        buildConfigField "String", "BASE_CLOUD_FUNC", "new String()"
        buildConfigField "String", "BASE_API_STICKER_PACK", "new String()"
        buildConfigField "String", "API_REQ_TRIAL_URI", "new String()"
        buildConfigField "String", "API_REQ_TRIAL_AUTH_TOKEN", "new String()"
        buildConfigField "String", "API_REQ_STICKER_PACK_TOKEN", "new String()"
        buildConfigField "String", "OPENSUBTITLES_API_KEY", "new String()"
    }

    defaultConfig {
        applicationId "com.amaze.fileutilities"
        minSdk 19
        targetSdk 31
        versionCode 70
        versionName "1.70"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (appProps != null &&
                appProps.containsKey('BASE_CLOUD_FUNC') &&
                appProps.containsKey('API_REQ_TRIAL_URI') &&
                appProps.containsKey('API_REQ_TRIAL_AUTH_TOKEN') &&
                appProps.containsKey('BASE_API_STICKER_PACK') &&
                appProps.containsKey('API_REQ_STICKER_PACK_TOKEN')) {
            buildConfigField "String", "BASE_CLOUD_FUNC", appProps["BASE_CLOUD_FUNC"]
            buildConfigField "String", "BASE_API_STICKER_PACK", appProps["BASE_API_STICKER_PACK"]
            buildConfigField "String", "API_REQ_TRIAL_URI", appProps["API_REQ_TRIAL_URI"]
            buildConfigField "String", "API_REQ_STICKER_PACK_TOKEN", appProps["API_REQ_STICKER_PACK_TOKEN"]
            buildConfigField "String", "API_REQ_TRIAL_AUTH_TOKEN", appProps["API_REQ_TRIAL_AUTH_TOKEN"]
        } else {
            println 'application.properties found but some entries are missing'
            buildConfigField "String", "BASE_CLOUD_FUNC", "new String()"
            buildConfigField "String", "BASE_API_STICKER_PACK", "new String()"
            buildConfigField "String", "API_REQ_TRIAL_URI", "new String()"
            buildConfigField "String", "API_REQ_STICKER_PACK_TOKEN", "new String()"
            buildConfigField "String", "API_REQ_TRIAL_AUTH_TOKEN", "new String()"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (appProps != null &&
                    appProps.containsKey('OPENSUBTITLES_API_KEY_DEBUG')) {
                buildConfigField "String", "OPENSUBTITLES_API_KEY", appProps["OPENSUBTITLES_API_KEY_DEBUG"]
            } else {
                buildConfigField "String", "OPENSUBTITLES_API_KEY", "new String()"
            }
            debuggable true //For "debug" banner on icon
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (appProps != null &&
                    appProps.containsKey('OPENSUBTITLES_API_KEY_RELEASE')) {
                buildConfigField "String", "OPENSUBTITLES_API_KEY", appProps["OPENSUBTITLES_API_KEY_RELEASE"]
            } else {
                buildConfigField "String", "OPENSUBTITLES_API_KEY", "new String()"
            }
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk false //generate an additional APK that contains all the ABIs
        }
    }

    packagingOptions {
        pickFirst '**/*.so'
        pickFirst 'versionchanges.txt'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/INDEX.LIST'
    }
    sourceSets {
        main {
            aidl {
                srcDirs 'src/main/aidl'
            }
        }
    }
}

play {
    serviceAccountCredentials.set(file("service_account_config.json"))
    defaultToAppBundles.set(true)

    track.set("internal")
//    track.set("production")
    userFraction.set(1.0d)
    updatePriority.set(3)
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.COMPLETED)
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidXLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidXLifecycleVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    //Glide: loads icons seemlessly
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation ("com.github.bumptech.glide:recyclerview-integration:$glideVersion") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    implementation 'com.google.android.play:core-ktx:1.8.1'
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
//    implementation 'com.github.barteksc:android-pdf-viewer:2.8.2'
    implementation 'com.github.barteksc:android-pdf-viewer:3.2.0-beta.1'
    implementation 'com.github.VishalNehra:FolioReader-Android:0.5.8'
    implementation 'org.zwobble.mammoth:mammoth:1.4.2'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.8'
    implementation "ch.acra:acra-core:5.7.0"

    // https://mvnrepository.com/artifact/com.drewnoakes/metadata-extractor
    implementation group: 'com.drewnoakes', name: 'metadata-extractor', version: '2.16.0'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-imaging
//    implementation group: 'org.apache.commons', name: 'commons-imaging', version: '1.0-alpha2'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.google.android.exoplayer:exoplayer:2.16.0'
    implementation 'com.google.android.exoplayer:extension-mediasession:2.16.0'
    implementation 'com.ramijemli.percentagechartview:percentagechartview:0.3.1'
    implementation 'com.github.massoudss:waveformSeekBar:5.0.1'
    implementation 'com.github.lincollincol:amplituda:2.1.6'
    //Simple library show
    implementation('com.mikepenz:aboutlibraries:6.1.1@aar') {
        transitive = true
    }
    implementation 'com.quickbirdstudios:opencv:4.5.3.0'
    // https://mvnrepository.com/artifact/net.sourceforge.tess4j/tess4j
//    implementation 'com.rmtheis:tess-two:9.1.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.0'
    implementation 'com.google.android.gms:play-services-mlkit-face-detection:17.0.1'
//    implementation 'com.google.mlkit:face-detection:16.1.5'

    implementation 'com.afollestad.material-dialogs:files:3.3.0'
    implementation 'androidx.mediarouter:mediarouter:1.2.6'
    implementation 'com.google.android.gms:play-services-cast-framework:21.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'com.stephentuso:welcome:1.4.1'
    implementation("com.android.billingclient:billing:$billing_version")
    implementation 'me.tankery.lib:circularSeekBar:1.4.0'
    implementation 'com.github.qoqa:glide-svg:2.0.4'
    api "com.caverock:androidsvg:1.4"
    implementation 'com.burhanrashid52:photoeditor:2.0.0'
    implementation 'com.github.CanHub:Android-Image-Cropper:4.3.1'

    //Detect memory leaks
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

jacoco {
    toolVersion = "${jacocoVersion}"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

Properties props = new Properties()
def propFile = new File('signing.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('STORE_FILE') &&
            props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') &&
            props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
