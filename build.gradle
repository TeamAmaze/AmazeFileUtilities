// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.21"
        classpath 'com.hiya:jacoco-android:0.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.spotless" version "5.14.2"
}

allprojects {
    tasks.withType(Test) {
        maxParallelForks = 4
        maxHeapSize = "4g"
        forkEvery = 4
        failFast = true
    }
}

spotless {
    java {
        target 'app/src/**/*.java'
        googleJavaFormat('1.7')
        removeUnusedImports() // removes any unused imports
        importOrder 'java', 'javax', 'org', 'com', 'android', 'androidx', ''
    }
    kotlin {
        target 'app/src/**/*.kt'
        ktlint("0.39.0").userData(['disabled_rules': 'no-wildcard-imports',
                                   'kotlin_imports_layout': 'idea', 'indent_size': '4',
                                   'max_line_length': '100'])
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}