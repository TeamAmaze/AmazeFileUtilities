// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.3.1"
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0'
        classpath 'com.hiya:jacoco-android:0.2'
        classpath "com.project.starter:easylauncher:6.1.0"
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:10.0.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.spotless" version "5.14.2"
}

allprojects {
    tasks.withType(Test) {
        maxParallelForks = 4
        maxHeapSize = "4g"
        forkEvery = 4
        failFast = true
    }
}

spotless {
    java {
        licenseHeaderFile 'spotless.license-java'
        target 'app/src/**/*.java'
        googleJavaFormat('1.13.0')
        removeUnusedImports() // removes any unused imports
        importOrder 'java', 'javax', 'org', 'com', 'android', 'androidx', ''
    }
    kotlin {
        licenseHeaderFile 'spotless.license-java'
        target 'app/src/**/*.kt'
        ktlint("0.43.2").userData(['disabled_rules': 'no-wildcard-imports',
                                   'kotlin_imports_layout': 'idea', 'indent_size': '4',
                                   'max_line_length': '100'])
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    format 'xml', {
        target 'app/src/main/res/values*/strings.xml'
        eclipseWtp('xml').configFile('spotless.xml.prefs')
    }
}